
#Область ПрограммныйИнтерфейс

// Процедура - Открыть отчёт с отборами
//
// Параметры:
//  ДанныеОтчета 	- Структура - Ключи "ИмяОтчета", "ИмяФормы", "ВариантОтчета" (строка) 
//  ПараметрыСКД 	- Структура - Ключ структуры - имя параметра (строка), значение структуры - значение параметра (произвольное значение)
//  ОтборыСКД	 	- Структура - Ключ структуры - имя параметра (строка), значение структуры - значение параметра (произвольное значение)
// 	ВладелецФормы 	- Форма или элемент управления другой формы.
//
Процедура ОткрытьОтчётСОтборами(ДанныеОтчета, ПараметрыСКД, ОтборыСКД, ВладелецФормы = Неопределено) Экспорт
	
	ПутьКФормеОтчета = "Отчет." + ДанныеОтчета.ИмяОтчета + "." + ДанныеОтчета.ИмяФормы;
	ФормаОтчёта = ПолучитьФорму(ПутьКФормеОтчета);
	КомпоновщикНастроек = ФормаОтчёта.Отчет.КомпоновщикНастроек;
	
	Если ТипЗнч(ПараметрыСКД) = Тип("Структура") И ПараметрыСКД.Количество() <> 0 Тогда
		Для Каждого КлючИЗначение Из ПараметрыСКД Цикл
			УстановитьПользовательскийПараметрСКД(КомпоновщикНастроек, КлючИЗначение.Ключ, КлючИЗначение.Значение);	
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(ОтборыСКД) = Тип("Структура") И ОтборыСКД.Количество() <> 0 Тогда
		Для Каждого КлючИЗначение Из ОтборыСКД Цикл
			УстановитьПользовательскийОтборСКД(КомпоновщикНастроек, КлючИЗначение.Ключ, КлючИЗначение.Значение);	
		КонецЦикла;
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("СформироватьПриОткрытии"	, Истина);
	ПараметрыФормы.Вставить("КлючВарианта"				, ДанныеОтчета.ВариантОтчета);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки"	, КомпоновщикНастроек.ПользовательскиеНастройки); 
	
	ОткрытьФорму(ПутьКФормеОтчета, ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры //  ОткрытьОтчётСОтборами

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьПользовательскийПараметрСКД(КомпоновщикНастроек, ИмяПараметра, Значение)
	
	Перем ПараметрДанных, Настройки, ПользовательскиеНастройки, ПользовательскийПараметр;
	
	Настройки = КомпоновщикНастроек.Настройки;
	
	ПараметрДанных = Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	
	Если ЗначениеЗаполнено(ПараметрДанных.ИдентификаторПользовательскойНастройки) Тогда
		
		ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
		ПользовательскийПараметр = ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		ПользовательскийПараметр.Использование = Истина;
		ПользовательскийПараметр.Значение = Значение;
	КонецЕсли;
	
КонецПроцедуры  //  УстановитьПользовательскийПараметрСКД

Процедура УстановитьПользовательскийОтборСКД(КомпоновщикНастроек, ИмяОтбора, Значение, ВидСравнения = Неопределено)
	
	Перем	ПолеКД, Элем, ЭлементОтбора, Настройки, 
			ПользовательскиеНастройки, ПользовательскийОтбор;
	
	Настройки = КомпоновщикНастроек.Настройки;
	
	ПолеКД = Новый ПолеКомпоновкиДанных(ИмяОтбора);
	
	Для Каждого Элем Из Настройки.Отбор.Элементы Цикл
		
		Если Элем.ЛевоеЗначение = ПолеКД Тогда
			ЭлементОтбора = Элем;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭлементОтбора = Неопределено Тогда
		Возврат;		
	КонецЕсли;		
	
	Если ЗначениеЗаполнено(ЭлементОтбора.ИдентификаторПользовательскойНастройки) Тогда
		
		ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
 		ПользовательскийОтбор = ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		ПользовательскийОтбор.Использование = (Значение <> Неопределено);
		ПользовательскийОтбор.ВидСравнения = ?(ВидСравнения <> Неопределено, ВидСравнения, ВидСравненияКомпоновкиДанных.Равно); 
		ПользовательскийОтбор.ПравоеЗначение  = Значение;
	КонецЕсли;

КонецПроцедуры //  УстановитьПользовательскийОтборСКД


#Область Примеры

Процедура ПРИМЕР_ОткрытьОтчётСОтборами(СсылкаНаДокумент)
	
	ДанныеОтчета = Новый Структура;
	ДанныеОтчета.Вставить("ИмяОтчета"		, "ОтчетПоЛимитам");
	ДанныеОтчета.Вставить("ИмяФормы"		, "Форма");
	ДанныеОтчета.Вставить("ВариантОтчета"	, "Основной");
	
	ПараметрыСКД = Новый Структура;
	ПараметрыСКД.Вставить("Организация", СсылкаНаДокумент.Организация);
	
	ОтборыСКД = Новый Структура;
	ОтборыСКД.Вставить("Контрагент"		, СсылкаНаДокумент.Контрагент);
	ОтборыСКД.Вставить("ОсновнойДоговор", СсылкаНаДокумент.ДоговорКонтрагента);
	
	дк_СКДКлиент.ОткрытьОтчётСОтборами(ДанныеОтчета, ПараметрыСКД, ОтборыСКД);  
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
